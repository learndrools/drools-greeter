package com.learndrools.greeting

import com.learndrools.greeting.Person.Gender;
import com.learndrools.greeting.Person.Education;
import com.learndrools.greeting.Person.Degree;


rule "GreetingGoodMorning"
    when
        thePerson : Person()
        tod : TimeOfDay(hour < 12)
        not GreetingsAndSalutations(recipient == thePerson, tod == tod)
    then
        GreetingsAndSalutations gns = new GreetingsAndSalutations(thePerson, tod, GreetingsAndSalutations.MORNING);
        insert(gns);
end

rule "GreetingGoodAfternoon"
    when
        thePerson : Person()
        tod : TimeOfDay(hour >= 12, hour <= 17)
        not GreetingsAndSalutations(recipient == thePerson, tod == tod)
    then
        GreetingsAndSalutations gns = new GreetingsAndSalutations(thePerson, tod, GreetingsAndSalutations.AFTERNOON);
        insert(gns);
end

rule "GreetingGoodEvening"
    when
        thePerson : Person()
        tod : TimeOfDay(hour >= 18, hour <= 22)
        not GreetingsAndSalutations(recipient == thePerson, tod == tod)
    then
        GreetingsAndSalutations gns = new GreetingsAndSalutations(thePerson, tod, GreetingsAndSalutations.EVENING);
        insert(gns);
end

rule "GreetingGoodNight"
    when
        thePerson : Person()
        tod : TimeOfDay(hour >= 23, hour <= 24)
        not GreetingsAndSalutations(recipient == thePerson, tod == tod)
    then
        GreetingsAndSalutations gns = new GreetingsAndSalutations(thePerson, tod, GreetingsAndSalutations.NIGHT);
        insert(gns);
end


rule "SalutationKnightedMale"
	when
		thePerson : Person(knighted == true, gender == Gender.MALE)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Sir");
		update(gns);
end

rule "SalutationKnightedFemale"
	when
		thePerson : Person(knighted == true, gender == Gender.FEMALE)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Dame");
		update(gns);
end

rule "PrenomialForPHD"
	when
		thePerson : Person(knighted == false, education == Education.PHD)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Dr.");
		update(gns);
end

rule "PrenomialForUnder10YearsOld"
	when
		thePerson : Person(knighted == false, education != Education.PHD, age <= 10)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Little");
		update(gns);
end

rule "DefaultPrenomialForMales"
	when
		thePerson : Person(knighted == false, education != Education.PHD, age > 10, gender == Gender.MALE)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Mr.");
		update(gns);
end

rule "PrenomialForSingleFemales"
	when
		thePerson : Person(knighted == false, education != Education.PHD, age > 10, gender == Gender.FEMALE, maritalStatus == MaritalStatus.SINGLE)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Ms.");
		update(gns);
end

rule "PrenomialForMarriedFemales"
	when
		thePerson : Person(knighted == false, education != Education.PHD, age > 10, gender == Gender.FEMALE, maritalStatus == MaritalStatus.MARRIED)
		gns : GreetingsAndSalutations(recipient == thePerson, prenomial == null)
	then
		gns.setPrenomial("Mrs.");
		update(gns);
end

rule "PostnomialForDegreeMD"
	when
		thePerson : Person(education >= Education.MASTERS, degree == Degree.MD)
		gns : GreetingsAndSalutations(recipient == thePerson, postnomial == null)
	then
	    gns.setPostnomial("M.D.");
        update(gns);
end

rule "PostnomialForDegreeMA"
	when
		thePerson : Person(education >= Education.MASTERS, degree == Degree.MA)
		gns : GreetingsAndSalutations(recipient == thePerson, postnomial == null)
	then
	    gns.setPostnomial("M.A.");
        update(gns);
end

rule "PostnomialForDegreeMFA"
	when
		thePerson : Person(education >= Education.MASTERS, degree == Degree.MFA)
		gns : GreetingsAndSalutations(recipient == thePerson, postnomial == null)
	then
	    gns.setPostnomial("M.F.A.");
        update(gns);
end

rule "PostnomialForDegreeMBA"
	when
		thePerson : Person(education >= Education.MASTERS, degree == Degree.MBA)
		gns : GreetingsAndSalutations(recipient == thePerson, postnomial == null)
	then
	    gns.setPostnomial("M.B.A.");
        update(gns);
end

query "AllGreetingsAndSalutations"
	gns : GreetingsAndSalutations()
end

